`timescale 1ns / 1ps
import Data_Structures::*;

module Top_of_Book_Engine_tb;

    // PARAMETERS
    localparam int N = 8;
    localparam int RAW_DATA_W = 98;
    localparam int PRICE_W = 32;
    localparam int TIMESTAMP_W = 32;
    localparam int SIZE_W = 32;
    localparam int LANE_W = 3;
    localparam int BID = 1;
    localparam int ASK = 0;

    localparam int PIPELINE_LATENCY = 20; // Safe margin

    // SIGNALS
    logic clk;
    logic rst_n;
    logic [RAW_DATA_W-1:0] in_data [N-1:0];

    quote_t best_bid, best_ask;
    logic signed [PRICE_W:0] out_spread;
    logic [PRICE_W-1:0] out_mid;
    logic out_cross;
    logic out_lock;

    // DUT
    TOB_Engine #(.N(N)) DUT (
        .clk(clk),
        .rst_n(rst_n),
        .in_data(in_data),
        .best_bid(best_bid),
        .best_ask(best_ask),
        .out_spread(out_spread),
        .out_mid(out_mid),
        .out_cross(out_cross),
        .out_lock(out_lock)
    );

    // CLOCK
    initial clk = 0;
    always #5 clk = ~clk;

    // PACK RAW DATA
    function automatic logic [RAW_DATA_W-1:0] pack_quote(
        input logic valid,
        input logic side,
        input logic [PRICE_W-1:0] price,
        input logic [TIMESTAMP_W-1:0] timestamp,
        input logic [SIZE_W-1:0] size
    );
        logic [RAW_DATA_W-1:0] tmp;
        tmp = '0;

        tmp[RAW_DATA_W-1] = valid;
        tmp[RAW_DATA_W-2] = side;
        tmp[RAW_DATA_W-3 -: PRICE_W] = price;
        tmp[RAW_DATA_W-3-PRICE_W -: TIMESTAMP_W] = timestamp;
        tmp[RAW_DATA_W-3-PRICE_W-TIMESTAMP_W -: SIZE_W] = size;

        return tmp;
    endfunction

    // RESET
    task reset_dut();
        rst_n = 0;
        repeat (5) @(posedge clk);
        rst_n = 1;
    endtask

    task clear_inputs();
        for (int i = 0; i < N; i++)
            in_data[i] = '0;
    endtask

    // REFERENCE MODEL
    function automatic quote_t select_best_bid(quote_t quotes[N]);
        quote_t best;
        best = '0;

        for (int i = 0; i < N; i++) begin
            if (!quotes[i].valid) continue;
            if (quotes[i].side != BID) continue;

            if (!best.valid)
                best = quotes[i];
            else if (
                (quotes[i].price > best.price) ||
                (quotes[i].price == best.price &&
                 quotes[i].timestamp > best.timestamp) ||
                (quotes[i].price == best.price &&
                 quotes[i].timestamp == best.timestamp &&
                 quotes[i].lane_id > best.lane_id)
            )
                best = quotes[i];
        end
        return best;
    endfunction

    function automatic quote_t select_best_ask(quote_t quotes[N]);
        quote_t best;
        best = '0;

        for (int i = 0; i < N; i++) begin
            if (!quotes[i].valid) continue;
            if (quotes[i].side != ASK) continue;

            if (!best.valid)
                best = quotes[i];
            else if (
                (quotes[i].price < best.price) ||
                (quotes[i].price == best.price &&
                 quotes[i].timestamp > best.timestamp) ||
                (quotes[i].price == best.price &&
                 quotes[i].timestamp == best.timestamp &&
                 quotes[i].lane_id > best.lane_id)
            )
                best = quotes[i];
        end
        return best;
    endfunction

    // CHECK TASK
    task automatic check_results(
        input quote_t exp_bid,
        input quote_t exp_ask
    );

        logic signed [PRICE_W:0] exp_spread;
        logic [PRICE_W-1:0] exp_mid;
        logic exp_cross;
        logic exp_lock;

        exp_spread = $signed({1'b0, exp_ask.price}) - $signed({1'b0, exp_bid.price});
        exp_cross = (exp_spread[PRICE_W]);
        exp_lock = (exp_spread == 0);
        exp_mid = (exp_bid.price + exp_ask.price) >> 1;

        if (best_bid.price !== exp_bid.price)
            $fatal("Best BID mismatch");

        if (best_ask.price !== exp_ask.price)
            $fatal("Best ASK mismatch");

        if (out_spread !== exp_spread)
            $fatal("Spread mismatch");

        if (out_mid !== exp_mid)
            $fatal("Midpoint mismatch");

        if (out_cross !== exp_cross)
            $fatal("Cross mismatch");
            
        if (out_lock != exp_lock)
            $fatal("Lock mismatch");

        $display("PASS");
    endtask

    // LATENCY MEASUREMENT
    task automatic wait_for_output(
        input quote_t exp_bid,
        input quote_t exp_ask
    );
        int latency = 0;

        while (best_bid.price !== exp_bid.price || best_ask.price !== exp_ask.price) begin
            @(posedge clk);
            latency++;

            if (latency > 100)
                $fatal("Timeout waiting for output");
        end

        $display("Measured latency = %0d cycles", latency);
    endtask

    // MAIN TEST
    initial begin
        quote_t model_quotes [0:N-1];
        quote_t exp_bid, exp_ask;

        clear_inputs();
        reset_dut();

        // TEST 1: Basic Selection
        model_quotes = '{default:'0};

        model_quotes[0] = '{valid:1, side:BID, price:100, timestamp:10, size:1, lane_id:0};
        model_quotes[1] = '{valid:1, side:BID, price:105, timestamp:12, size:1, lane_id:1};
        model_quotes[2] = '{valid:1, side:ASK, price:110, timestamp:15, size:1, lane_id:2};
        model_quotes[3] = '{valid:1, side:ASK, price:108, timestamp:14, size:1, lane_id:3};

        for (int i = 0; i < N; i++) begin
            if (model_quotes[i].valid)
                in_data[i] = pack_quote(
                    model_quotes[i].valid,
                    model_quotes[i].side,
                    model_quotes[i].price,
                    model_quotes[i].timestamp,
                    model_quotes[i].size
                );
        end

        exp_bid = select_best_bid(model_quotes);
        exp_ask = select_best_ask(model_quotes);

        wait_for_output(exp_bid, exp_ask);
        check_results(exp_bid, exp_ask);

        // TEST 2: Cross
        reset_dut();
        clear_inputs();
        model_quotes = '{default:'0};

        model_quotes[0] = '{valid:1, side:BID, price:110, timestamp:20, size:1, lane_id:0};
        model_quotes[1] = '{valid:1, side:ASK, price:105, timestamp:21, size:1, lane_id:1};

        for (int i = 0; i < N; i++) begin
            if (model_quotes[i].valid)
                in_data[i] = pack_quote(
                    model_quotes[i].valid,
                    model_quotes[i].side,
                    model_quotes[i].price,
                    model_quotes[i].timestamp,
                    model_quotes[i].size
                );
        end

        exp_bid = select_best_bid(model_quotes);
        exp_ask = select_best_ask(model_quotes);

        wait_for_output(exp_bid, exp_ask);
        check_results(exp_bid, exp_ask);

        // TEST 3: Timestamp Tie
        reset_dut();
        clear_inputs();
        model_quotes = '{default:'0};

        model_quotes[0] = '{valid:1, side:BID, price:100, timestamp:10, size:1, lane_id:0};
        model_quotes[1] = '{valid:1, side:BID, price:100, timestamp:20, size:1, lane_id:1};
        model_quotes[2] = '{valid:1, side:ASK, price:200, timestamp:10, size:1, lane_id:2};

        for (int i = 0; i < N; i++) begin
            if (model_quotes[i].valid)
                in_data[i] = pack_quote(
                    model_quotes[i].valid,
                    model_quotes[i].side,
                    model_quotes[i].price,
                    model_quotes[i].timestamp,
                    model_quotes[i].size
                );
        end

        exp_bid = select_best_bid(model_quotes);
        exp_ask = select_best_ask(model_quotes);

        wait_for_output(exp_bid, exp_ask);
        check_results(exp_bid, exp_ask);

        $display("ALL TESTS PASSED SUCCESSFULLY");

        $finish;
    end

endmodule
